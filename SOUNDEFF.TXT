; >s:sound-effects
; put"sound-effects"
;
; an attempt to write a nice
; routine like our mate robbie
; does.
;
sfx
;
;  call with acc set to sfx #
;
; this will clear the last sfx
;
; 0=fire         ;* chan#2
; 1=explosion    ;*   "
; 2=fuel low     ;* on channel 1
; 3=pow sound    ;* on channel 2
; 4=ship dead    ;* on channel 2
;
ldy sndflg       ;* quiet
bne gofx
;
rts
;
gofx
;
tax
inx
;
cmp #2
bne gensis
;
lda #$80
ora fxnumb
tax
;
; set top bit for #2
;
gensis
;
stx fxnumb       ;* # + 1
;
lda #%00001111
sta welly        ;* volume
;
cpx #1
bne sf1
;
;---------------------------------
;
; set up firing sound
;
lda #$10
sta fxcont
;
lda #0
sta fxwork
;
lda #$7f
sta sou2lo
sta sou2hi
;
rts
;
;---------------------------------
;
; set up explosion sound
;
sf1
;
cpx #2
bne sf2
;
lda #12
sta fxcont
;
lda #$ff
sta sou2lo
sta sou2hi
;
rts
;
;---------------------------------
;
; set up low fuel sound
;
sf2
;
bit fxnumb     ;* fuel low ?
bpl sf3
;
lda #1
sta fxwork+1
;
sta fxtemp+1
sta sou1lo
;
lda sou1hi
and #%11111100
ora #%00000010
sta sou1hi
;
rts
;
;---------------------------------
;
sf3
;
cpx #4
bne sf4
;
; set up pow sfx
;
lda #$18
sta fxcont
;
lda #0
sta sou2lo
sta sou2hi
;
rts
;
;---------------------------------
;
sf4
;
cpx #5
bne sf5
;
; start long explosion of ship
;
lda #96
sta fxcont
;
rts
;
;---------------------------------
;
sf5
;
rts
;
;---------------------------------
;*********************************
;---------------------------------
;
refrsh
;
; refresh sound effects
;
lda sndflg        ;* sound on ?
bne sndon
;
nosnd
;
lda volctl
bne nisnd         ;* leave vol.
;
lda #0
sta vol
;
nisnd
;
rts
;
sndon
;
lda fxnumb
beq nosnd         ;* none on
;
and #$7f
;
cmp #1
bne sou2
;
;---------------------------------
;
; service sfx #0
;
dec fxcont        ;* length
bne sfx1on
;
jsr endn2         ;* end chan#2
jmp sou2
;
sfx1on
;
lda fxwork
eor #$80
sta fxwork        ;* other one
;
s1
;
ldx #%01000000    ;* noise
;
bit fxwork
bpl noise
;
ldx #%00100000    ;* normal
;
noise
;
lda sou1lo
sec
sbc #4
sta sou1lo
;
txa
jsr stch2         ;* start sound
;
;---------------------------------
;
sou2
;
lda fxnumb
and #$7f
;
cmp #2
bne sou3
;
dec fxcont
bne sfx2on
;
jsr endn2         ;* kill it
jmp sou3
;
sfx2on
;
lda ted
sta sou2lo
;
lda #%01000000    ;* noise #2
jsr stch2
;
;---------------------------------
;
sou3
;
bit fxnumb
bpl sou4        ;* not fuel low
;
; low fuel sound
;
dec fxtemp+1
bne sameb4
;
lda #4
sta fxtemp+1
;
lda fxwork+1
eor #$80
sta fxwork+1
;
sameb4
;
bit fxwork+1
bpl rigt
;
clc
rol sou1lo
;
jmp rope
;
rigt
;
clc
ror sou1lo
;
rope
;
lda vol
and #%01100000
ora #%00010000     ;* chan#1
ora welly          ;* volume
sta vol
;
;---------------------------------
;
sou4
;
lda fxnumb
and #$7f
cmp #4
bne sou5
;
dec fxcont
bne sfx4on
;
jsr endn2
jmp sou5
;
sfx4on
;
lda sou2lo
clc
adc #$40
sta sou2lo
;
lda sou2hi
adc #0
sta sou2hi
;
lda #%00100000    ;* square wave
jsr stch2
;
;---------------------------------
;
sou5
;
lda fxnumb
and #$7f
cmp #5
bne sou6
;
dec fxcont
bne sfx5on
;
jsr endn2
jmp sou6
;
sfx5on
;
lda fxcont
cmp #64
bcs six4    ;* dont start decay
;
lsr a
lsr a
sta welly   ;* decay volume
;
six4
;
lda ted
sta sou2hi
lda ted+1
sta sou2lo
;
lda #%01000000    ;* noise
jsr stch2
;
;---------------------------------
;
sou6
;
;
rts
;
;---------------------------------
;
stch2
;
; call with acc, to store at vol.
;
sta irqwrk
;
lda vol
and #%00010000    ;* leave vol/#1
ora welly         ;* volume
ora irqwrk
sta vol
;
rts
;
;---------------------------------
;
endn2
;
; end a sound on channel 2
;
lda fxnumb
and #$80
sta fxnumb
;
; low fuel might still be on
;
lda vol
and #%00011111
sta vol
;
; turn off anything to do with #2
;
rts
;
;---------------------------------
;
endn1
;
; end a sound on channel 1
;
lda fxnumb
and #$7f
sta fxnumb
;
; other sounds might still be on
;
lda vol
and #%01101111
sta vol
;
; turn off anything to do with #1
;
rts
;
;---------------------------------
;
;
;
.end
