; >s:star-code
; put "star-code"
;
;*********************************
;
; all those wonderfull
;  3 - speed stars
;
;*********************************
;
;     init & define star table
;
;*********************************
;
; table format :-
;
; byte #
;
;   0     x position 0-159
;   1     y position 0-15 (window)
;
instar
;
lda #0    ;* star count
sta numb
;
st1
;
jsr random
;
ldx numb
;
lda rnd+1
adc ted
;
pana
;
adc ted+1
cmp #159
bcs pana
;
sta starxp,x    ;* 0 - 159
;
lda rnd+2
and #15         ;* 0 - 15
sta staryp,x    ;* y pos
;
lda rnd+3
and #3          ;* speed
sta starsp,x
;
inc numb
lda numb
cmp #starnm
;
bne st1
;
rts
;
;***************************
;
; rubout,move and print stars
;
;***************************
;
stars
;
lda stpstr      ;* stars ?
beq gostar
;
rts
;
gostar
;
lda #0
sta numb
;
mvestr
;
ldx numb
;
lda starsp,x
tay
;
clc
adc #1
sta speed       ;* 1,2,3,4 speed
;
tya
pha
;
lda stcols,y    ;* colours
sta stcol
;
lda staryp,x    ;* y pos
sta ty
;
jsr starxy      ;* get screen add.
;
lda starxp,x    ;* x pos
sta tx
;
pla
tay
;
lda ststop,y
bne statnw      ;* stop this one
;
lda tx
lsr a
lsr a
tay
;
lda (sl),y
cmp #102
bcc fukoff
;
cmp #106
bcs fukoff
;
lda #32
sta (sl),y     ;* rub it out
;
fukoff
;
lda tx         ;* x pos
sec
sbc speed
sta tx
;
cmp #4
bcs statnw     ;* start new ?
;
; start a new star
;
jsr random
;
lda rnd+1
and #15
sta ty         ;* new y pos
;
lda #159       ;* x to right
sta tx
;
ldx numb       ;* pointer
lda rnd+2      ;* speed
and #3
sta starsp,x
;
tay
iny
sty speed      ;* set up var
;
statnw
;
ldx numb       ;* pointer
;
lda tx
sta starxp,x   ;* x pos
;
lda ty
sta staryp,x   ;* y pos
;
; new values into table
;
jsr starxy    ;* calc column
;
lda tx        ;* x pos
lsr a
lsr a         ;* /4
tay
;
lda (sl),y
cmp #32
beq prtit     ;* print star
;
cmp #102
bcc dntprt    ;* dont print
;
cmp #106
bcs dntprt    ;* too big
;
prtit
;
lda tx        ;* from the lowest
and #3        ;* 3 bits calculate
clc           ;* the star char.
adc #102      ;* = base char
sta (sl),y
;
lda sl+1
sec
sbc #4
sta sl+1      ;* calc colour
;
lda stcol     ;* star colour
sta (sl),y
;
dntprt
;
inc numb
lda numb
cmp #starnm
beq mveend
;
jmp mvestr
;
mveend
;
rts
;
;---------------------------------
;
starxy
;
and #15        ;* be safe
asl a
tay
;
lda scrpos,y
sta sl
;
lda scrpos+1,y
sta sl+1
;
rts
;
;---------------------------------
;
stcols
;
.byte $41,$51,$61,$71
;
.end
